// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
DukeMurderThreeQuest is DukeMurderQuestTemplate
// Quest template QST_ID_MURDER_DUKE_3 = 65.
// Quest nodes 187, 188, 191.
// QNT_ID_DUKE_MURDER_ONE, QNT_ID_DUKE_MURDER_TWO, QNT_ID_DUKE_MURDER_THREE_THREE

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   // Quest description.

   // Quest resources.

classvars:

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_MURDER_DUKE_3

properties:

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_DUKE_MURDER_ONE, QNT_ID_DUKE_MURDER_TWO,
                      QNT_ID_DUKE_MURDER_THREE_THREE];

      // Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_DUKE_MURDER_TWO, vrNodeTwo],
                          [QNT_ID_DUKE_MURDER_THREE_THREE, vrNodeThree] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local i, lCargo, lCargo2, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      lCargo  = [ murder_maleval_w_1, murder_maleval_w_2, murder_maleval_w_3,
                  murder_maleval_w_4, murder_maleval_w_5, murder_maleval_m_1,
                  murder_maleval_m_2, murder_maleval_m_3, murder_maleval_m_4,
                  murder_maleval_m_5, murder_maleval_c_1, murder_maleval_c_2,
                  murder_maleval_c_3, murder_maleval_c_4, murder_maleval_c_5,
                  murder_roq_w_1, murder_roq_w_2, murder_roq_w_3,
                  murder_roq_w_4, murder_roq_w_5, murder_roq_m_1,
                  murder_roq_m_2, murder_roq_m_3, murder_roq_m_4,
                  murder_roq_m_5 ];
      lCargo2 = [ murder_roq_c_1, murder_roq_c_2, murder_roq_c_3,
                  murder_roq_c_4, murder_roq_c_5, murder_random_w_1,
                  murder_random_m_1, murder_random_c_1 ];
      foreach i in lCargo2
      {
         lCargo = Cons(i, lCargo);
      }

      // Template #191 is Duke's Murder Mystery quest, come back and give the info
      // QNT_ID_DUKE_MURDER_THREE_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=lCargo,#timelimit=3 * 3600,
               #prizelist=[ [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 500 ] ],
               #quest_node_index=QNT_ID_DUKE_MURDER_THREE_THREE)
      {
         Send(oQE,@SetQuestNodeNPCModifier,#index=QNT_ID_DUKE_MURDER_THREE_THREE,
               #new_mod=QN_NPCMOD_PREVIOUS);
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_DUKE_MURDER_THREE_THREE,
               #new_hint=murder_say);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_DUKE_MURDER_THREE_THREE,
               #new_hint=murder_duke_success);
      }
      else
      {
         Debug("Error building QN: QNT_ID_DUKE_MURDER_THREE_THREE");
      }

      propagate;
   }

end
/////////////////////////////////////////////////////////////////////////////////
