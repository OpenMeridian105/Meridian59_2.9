// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
KocAleQuest is QuestTemplate
// Quest template QST_ID_KOC_ALE_TRADE = 55.
// Quest nodes 168, 169, 170.
// QNT_ID_KOC_ALE_TRADE_ONE, QNT_ID_KOC_ALE_TRADE_TWO,
// QNT_ID_KOC_ALE_TRADE_THREE

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   include kocaleqt.lkod

   // Quest description.
   kocaleqt_icon_rsc = mug.bgf
   kocaleqt_name_rsc = "Booze Delivery"
   kocaleqt_desc_rsc = \
      "%s needs a guaranteed delivery of alcohol made to the Island.  If you "
      "are willing to make the journey for them, you would be rewarded well "
      "at the destination."

   kocaleqt_nodetwo_rsc = \
      "%s%s needs a guaranteed delivery made to the island.  Failure to "
      "make the delivery would be punished. Will you accept these terms?"

   kocaleqt_nodethree_rsc = \
      "Deliver %i %s to %s%s.  Do not be tempted to keep the supplies "
      "for yourself; thievery will be punished."

   kocaleqt_completed_rsc = \
      "You have successfully completed a booze delivery to the island, and "
      "helped the weary Ko'catani forget their troubles for a while longer."

   // Quest resources.
   kocale_assign_2 = \
      "I need a guaranteed delivery made to the island.  Failure to deliver it "
      "would result in outlaw status.  Are you willing to accept these terms?"
   kocale_assign_2_h = \
      "I need a guaranteed delivery made to the island.  Failure to deliver "
      "it would be very unfortunate for you.  Are you willing to accept "
      "these terms?"
   kocale_failure_2 = \
      "Subject: I shall seek another courier\n"
      "I understand your reluctance to commit yourself to such a long "
      "journey.  Thank you anyway."
   kocale_assign_3 = \
      "%NPC in Kocatan has asked for supplies of our brew for his customers "
      "on the other side.  Please take these %INDEF_CARGO%CARGO to "
      "%HIMHER_NPC.  I have arranged for %NPC to pay you for the journey.  Do "
      "not steal my goods.  Not only will it brand you an outlaw but I also "
      "have a friend, a wizard, very proficient in curses.  But let us not "
      "dwell on the seedier side of life.  This could be the beginning of "
      "something very profitable for all involved, my friend!"
   kocale_assign_3_h = \
      "%NPC in Kocatan has asked for supplies of our brew for his customers "
      "on the other side.  Please take these %INDEF_CARGO%CARGO to "
      "%HIMHER_NPC.  I have arranged for %NPC to pay you for the journey.  Do "
      "not steal my goods;  I have a friend, a wizard, very proficient in "
      "curses.  But let us not dwell on the seedier side of life.  This could "
      "be the beginning of something very profitable for all involved, "
      "my friend!"
   kocale_failure_3 = \
      "Subject: Thief!\n"
      "Your thievery has made you an outlaw by decree of the Duke."
   kocale_failure_3_h = \
      "Subject: Thief!\n"
      "The bad luck brought on by your evil deed shall now follow you "
      "wherever you go."
   kocale_success_3 = \
      "I see %SOURCE_NPC has kept his word.  This is very good.  I am sure "
      "that this is the beginning of a mutually beneficial relationship.  "
      "Here is a token of my gratitude."

classvars:

   vrIcon = kocaleqt_icon_rsc
   vrName = kocaleqt_name_rsc
   vrDesc = kocaleqt_desc_rsc

   vrNodeTwo = kocaleqt_nodetwo_rsc
   vrNodeThree = kocaleqt_nodethree_rsc

   vrCompleted = kocaleqt_completed_rsc

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_KOC_ALE_TRADE

   viIcon_Animation_start = 1

properties:

   piNumPlayers = 1
   piPlayerRestrict = Q_PLAYER_NOTNEWBIE | Q_PLAYER_LAWFUL \
                      | Q_PLAYER_NOTTRIED_RECENTLY
   plSpecialRestrict = $
   piMaxPlayers = 6
   piSchedulePct = 100

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_KOC_ALE_TRADE_ONE, QNT_ID_KOC_ALE_TRADE_TWO,
                      QNT_ID_KOC_ALE_TRADE_THREE];

      // Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_KOC_ALE_TRADE_TWO, vrNodeTwo],
                          [QNT_ID_KOC_ALE_TRADE_THREE, vrNodeThree] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oNPC, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #168 is kocatan ale trade quest, say "need a courier"
      // QNT_ID_KOC_ALE_TRADE_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_NONE,#cargolist=[ letter_trigger ],
               #quest_node_index=QNT_ID_KOC_ALE_TRADE_ONE)
      {
         lNPCs = $;
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&BarloqueBartender)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&JasperBartender)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&TosInnKeeper)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&MarionBartender)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&CorNothInnkeeper)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_KOC_ALE_TRADE_ONE,
               #new_NPC_list=lNPCs);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KOC_ALE_TRADE_ONE");
      }

      // Template #169 is kocatan ale trade quest, say "yes"
      // QNT_ID_KOC_ALE_TRADE_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[ chess_trigger ],
               #timelimit=2 * 60,#quest_node_index=QNT_ID_KOC_ALE_TRADE_TWO)
      {
         // Happy land subs.
         if NOT Send(SYS,@IsPKAllowed)
         {
            Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KOC_ALE_TRADE_TWO,
                  #new_hint=kocale_assign_2_h);
         }
         else
         {
            // Use happyland subs anyway - no more outlaw punishment.
            Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KOC_ALE_TRADE_TWO,
                  #new_hint=kocale_assign_2_h);
         }
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KOC_ALE_TRADE_TWO,
               #new_hint=kocale_failure_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KOC_ALE_TRADE_TWO");
      }

      // Template #170 is kocatan ale trade quest
      // QNT_ID_KOC_ALE_TRADE_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_ITEMCLASS,
               #NPC_modifier=QN_NPCMOD_NONE,
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &Goblet, 50 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &StoutGoblet, 50 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &WineGoblet, 40 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &Mug, 50 ] ],
               #prizelist=[ [ [ QN_PRIZETYPE_ITEMCLASS, &InkyCap, 15 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &LongBow, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &Arrow, 60 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &NeruditeArrow, 40 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &SilverArrow, 40 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &KaraholsCursePotion, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &HoldWand, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &FireRing, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &ColdRing, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &ShockRing, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &AcidRing, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &ResistMagicPotion, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &ResistPoisonPotion, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &ResistShockPotion, 1 ] ] ],
               #penaltylist=[ [ QN_PRIZETYPE_OUTLAW ] ],
               #timelimit=4 * 3600,#quest_node_index=QNT_ID_KOC_ALE_TRADE_THREE)
      {
         lNPCs = $;
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&KocatanBartender)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_KOC_ALE_TRADE_THREE,
               #new_NPC_list=lNPCs);

         // Happy land subs.
         if NOT Send(SYS,@IsPKAllowed)
         {
            Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KOC_ALE_TRADE_THREE,
                  #new_hint=kocale_assign_3_h);
            Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KOC_ALE_TRADE_THREE,
                  #new_hint=kocale_failure_3_h);
         }
         else
         {
            // Use happyland subs anyway - no more outlaw punishment.
            Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_KOC_ALE_TRADE_THREE,
                  #new_hint=kocale_assign_3_h);
            Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_KOC_ALE_TRADE_THREE,
                  #new_hint=kocale_failure_3_h);
         }
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_KOC_ALE_TRADE_THREE,
               #new_hint=kocale_success_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_KOC_ALE_TRADE_THREE");
      }

      return;
   }

   ShowQuestUIDescRsc(who = $, oNPC = $)
   {
      local rName;

      if (oNPC = $)
      {
         Debug("Missing NPC while building quest UI description!");
         rName = system_blank_resource;
      }
      else
      {
         rName = Send(oNPC,@GetName);
      }

      AddPacket(4,vrDesc, 4,rName);

      return;
   }

   ExtraDesc(index=$,oQuestNode=$)
   {
      local iNum, oCargo, oDestNPC;

      if oQuestNode = $
         OR index = $
      {
         return;
      }

      oDestNPC = Send(oQuestNode,@GetDestNPC);

      if index = QNT_ID_KOC_ALE_TRADE_TWO
      {
         AddPacket(4,Send(oDestNPC,@GetCapDef), 4,Send(oDestNPC,@GetName));
      }

      if index = QNT_ID_KOC_ALE_TRADE_THREE
      {
         oCargo = Send(oQuestNode,@GetCargo);
         iNum = Send(oCargo,@GetNumber);

         AddPacket(4,iNum, 4,Send(oCargo,@GetName), 4,Send(oDestNPC,@GetDef),
                   4,Send(oDestNPC,@GetName));
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
