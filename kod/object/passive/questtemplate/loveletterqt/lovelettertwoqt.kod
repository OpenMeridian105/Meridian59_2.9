// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
LoveLetterTwoQuest is LoveLetterQuestTemplate
// Quest template QST_ID_LOVE_LETTER_2 = 43.
// Quest nodes 137, 138.
// QNT_ID_LOVE_LETTERTWO_ONE, QNT_ID_LOVE_LETTERTWO_TWO

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   include lovelettertwoqt.lkod

   // Quest description.
   lovelettertwoquest_name_rsc = "In the Service of Love"
   lovelettertwoquest_desc_rsc = \
      "Madelia has a penchant for writing romantic letters to her love "
      "interests.  Delivering one of these letters would net you a small reward."

   lovelettertwoquest_deliver_rsc = \
      "Madelia has asked you to deliver a love letter to %s%s."

   lovelettertwoquest_completed_rsc = \
      "You have delivered a love letter and helped romance blossom."

   // Quest resources.
   love_letter_2_assign = \
      "Yes, actually, I have a missive in need of delivery.  If thou "
      "wouldst give this letter to %NPC I would be most grateful."
   love_letter_2_success = \
      "Ah, a letter from my fair %SOURCE_NPC!  It warms my heart to know "
      "that she is thinking of me.  Please take this as a token of my thanks."

classvars:

   vrName = lovelettertwoquest_name_rsc
   vrDesc = lovelettertwoquest_desc_rsc

   vrDeliver = lovelettertwoquest_deliver_rsc
   vrCompleted = lovelettertwoquest_completed_rsc

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_LOVE_LETTER_2

properties:

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_LOVE_LETTERTWO_ONE,
                      QNT_ID_LOVE_LETTERTWO_TWO];

      // Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_LOVE_LETTERTWO_TWO, vrDeliver] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oNPC, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #137 is love letter quest 2
      // QNT_ID_LOVE_LETTERTWO_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=[ letter_trigger ],
               #quest_node_index=QNT_ID_LOVE_LETTERTWO_ONE)
      {
         lNPCs = $;
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&Barloquetailor)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_LOVE_LETTERTWO_ONE,
               #new_NPC_list=lNPCs);
      }
      else
      {
         Debug("Error building QN: QNT_ID_LOVE_LETTERTWO_ONE");
      }

      // Template #138 is love letter quest 2
      // QNT_ID_LOVE_LETTERTWO_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_ITEM,
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &LoveLetter, 1 ] ],
               #prizelist=[ [ [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 500 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 600 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 400 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 700 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 550 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 650 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 450 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Key, 1 ] ] ],
               #timelimit=6 * 3600,#quest_node_index=QNT_ID_LOVE_LETTERTWO_TWO)
      {
         lNPCs = $;
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&GuildCreator)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&BarloqueInnkeeper)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&RebelLiege)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_LOVE_LETTERTWO_TWO,
               #new_NPC_list=lNPCs);
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_LOVE_LETTERTWO_TWO,
               #new_hint=love_letter_2_assign);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_LOVE_LETTERTWO_TWO,
               #new_hint=love_letter_2_success);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_LOVE_LETTERTWO_TWO,
               #new_hint=love_letter_failure);
      }
      else
      {
         Debug("Error building QN: QNT_ID_LOVE_LETTERTWO_TWO");
      }

      return;
   }

   ExtraDesc(index=$,oQuestNode=$)
   {
      local oDestNPC;

      if oQuestNode = $
         OR index = $
      {
         return;
      }

      if index = QNT_ID_LOVE_LETTERTWO_TWO
      {
         oDestNPC = Send(oQuestNode,@GetDestNPC);

         AddPacket(4,Send(oDestNPC,@GetDef), 4,Send(oDestNPC,@GetName));
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
