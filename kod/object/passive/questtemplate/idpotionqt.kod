// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
IdentifyPotionQuest is QuestTemplate
// Quest template QST_ID_POTION_ID = 58.
// Quest nodes 176, 177, 178.
// QNT_ID_APOTHECARY_ONE, QNT_ID_APOTHECARY_TWO

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   include idpotionqt.lkod

   // Quest description.
   idpotionqt_icon_rsc = potion01.bgf
   idpotionqt_name_rsc = "The Mysterious Potion"
   idpotionqt_desc_rsc = \
      "%s recently received a potion they are unable to identify by magical "
      "means, and would like you to take it to an alchemical expert to "
      "determine its nature.  Returning the identified potion would net you "
      "a small reward."

   idpotionqt_nodetwo_rsc = \
      "%s%s recently received a potion %s is unable to identify by magical "
      "means, and would like you to take it to an alchemical expert to "
      "determine its nature.  Offer the potion to %s%s and see if %s can "
      "help with this task."

   idpotionqt_nodethree_rsc = \
      "%s%s has determined the nature of the potion. Go back to %s%s and "
      "offer %s the now identified potion, and collect your reward."

   idpotionqt_completed_rsc = \
      "You have helped identify a mysterious potion for a small reward."

   // Quest resources.
   potion_identify_assign_1 = \
      "As a matter of fact, yes!  I recently received this potion.  I tried "
      "to examine it magically, but it seems to repel my efforts.  Could "
      "you take it to %NPC and ask %HIMHER_NPC to examine it for me?  "
      "Come back with the knowledge of what it is and I'll reward you."
   potion_identify_assign_2 = \
      "Hmmm, a most interesting specimen.  Let me just take a few drops for "
      "analysis...  Ah yes, this appears to be a %CARGO.  Let me put a label "
      "on it so that %NPC will know what it is."
   potion_identify_success = \
      "Excellent, I'm sure I'll be able to find a good use for a %CARGO.  "
      "Here, please take this in payment."
   potion_identify_failure_1 = \
      "Subject: That strange potion\n"
      "Did you get lost?  Don't worry, that happens often to people that are "
      "new to the area.  The potion probably was not very important anyway.  "
      "I might have other jobs for you if you visit again in the future.  "
      "Take care!"
   potion_identify_failure_2 = \
      "Subject: That strange potion\n"
      "Did you get lost?  Don't worry, that happens often to people that are "
      "new to the area.  %NPC contacted me and said the potion wasn't "
      "important anyway.  Feel free to throw that %CARGO away, as it is way "
      "beyond its expiration date by now."

classvars:

   vrIcon = idpotionqt_icon_rsc
   vrName = idpotionqt_name_rsc
   vrDesc = idpotionqt_desc_rsc

   vrNodeTwo = idpotionqt_nodetwo_rsc
   vrNodeThree = idpotionqt_nodethree_rsc

   vrCompleted = idpotionqt_completed_rsc

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_POTION_ID

   viIcon_Animation_start = 1

properties:

   piNumPlayers = 1
   piPlayerRestrict = Q_PLAYER_NEWBIE | Q_PLAYER_NOTTRIED_RECENTLY
   plSpecialRestrict = $
   piMaxPlayers = 15
   piSchedulePct = 100

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_POTION_ID_ONE, QNT_ID_POTION_ID_TWO,
                      QNT_ID_POTION_ID_THREE];

      // Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_POTION_ID_TWO, vrNodeTwo],
                          [QNT_ID_POTION_ID_THREE, vrNodeThree] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oNPC, oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #176 is potion ID quest kickoff, "need a courier"
      // QNT_ID_POTION_ID_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #cargolist=[ letter_trigger ],
               #quest_node_index=QNT_ID_POTION_ID_ONE)
      {
         lNPCs = $;
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&CornothGrocer)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&CorNothSergeant)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&JasperMerchant)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&JasperBanker)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&MarionInnkeeper)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&MarionElder)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&TosInnKeeper)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_POTION_ID_ONE,
               #new_NPC_list=lNPCs);
      }
      else
      {
         Debug("Error building QN: QNT_ID_POTION_ID_ONE");
      }

      // Template #177 is potion ID quest, deliver the mystery potion
      // QNT_ID_POTION_ID_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_ITEM,
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &MysteryPotion, 1 ] ],
               #timelimit=6 * 3600,#quest_node_index=QNT_ID_POTION_ID_TWO)
      {
         lNPCs = $;
         lNPCs = Cons(First(Send(oLib,@GetOccupationList,
                     #cNPC_class=&BarloqueApothecary)),lNPCs);
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_POTION_ID_TWO,
               #new_NPC_list=lNPCs);
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_POTION_ID_TWO,
               #new_hint=potion_identify_assign_1);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_POTION_ID_TWO,
               #new_hint=potion_identify_failure_1);
      }
      else
      {
         Debug("Error building QN: QNT_ID_POTION_ID_TWO");
      }

      // Template #178 is potion ID quest, come back and give the labeled potion
      // QNT_ID_POTION_ID_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_ITEM,
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &LabeledPotion, 1 ] ],
               #prizelist=[ [ [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 5000 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Shillings, 8000 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &InkyCap, 8 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &Cheese, 6 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &ShortSword, 1 ],
                              [ QN_PRIZETYPE_ITEMCLASS, &GoldShield, 1 ] ] ],
               #timelimit=3 * 3600,#quest_node_index=QNT_ID_POTION_ID_THREE)
      {
         Send(oQE,@SetQuestNodeNPCModifier,#index=QNT_ID_POTION_ID_THREE,
               #new_mod=QN_NPCMOD_PREVIOUS);
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_POTION_ID_THREE,
               #new_hint=potion_identify_assign_2);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_POTION_ID_THREE,
               #new_hint=potion_identify_success);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_POTION_ID_THREE,
               #new_hint=potion_identify_failure_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_POTION_ID_THREE");
      }

      return;
   }

   ShowQuestUIDescRsc(who = $, oNPC = $)
   {
      local rName;

      if (oNPC = $)
      {
         Debug("Missing NPC while building quest UI description!");
         rName = system_blank_resource;
      }
      else
      {
         rName = Send(oNPC,@GetName);
      }

      AddPacket(4,vrDesc, 4,rName);

      return;
   }

   ExtraDesc(index=$,oQuestNode=$)
   {
      local oDestNPC, oSourceNPC;

      if oQuestNode = $
         OR index = $
      {
         return;
      }

      oDestNPC = Send(oQuestNode,@GetDestNPC);
      oSourceNPC = Send(oQuestNode,@GetSourceNPC);

      if index = QNT_ID_POTION_ID_TWO
      {
         AddPacket(4,Send(oSourceNPC,@GetCapDef), 4,Send(oSourceNPC,@GetName),
                   4,Send(oDestNPC,@GetHeShe), 4,Send(oDestNPC,@GetDef),
                   4,Send(oDestNPC,@GetName), 4,Send(oDestNPC,@GetHeShe));
      }

      if index = QNT_ID_POTION_ID_THREE
      {
         AddPacket(4,Send(oSourceNPC,@GetCapDef), 4,Send(oSourceNPC,@GetName),
                   4,Send(oDestNPC,@GetDef), 4,Send(oDestNPC,@GetName),
                   4,Send(oDestNPC,@GetHimHer));
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
