// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Dazzle is Debuff

constants:

   include blakston.khd

resources:

   include dazzle.lkod

   Dazzle_name_rsc = "dazzle"
   Dazzle_icon_rsc = idazzle.bgf
   Dazzle_desc_rsc = \
      "Dazzles the target with the blinding light of Shal'ille's goodness."
   Dazzle_caster = "%s%s is now dazzled."
   Dazzle_already_enchanted = "%s%s is already dazzled."
   Dazzle_on = "The world is washed away in a flood of pure light."
   Dazzle_off = "Your vision begins to clear."
   Dazzle_sound = sDazzle.ogg

classvars:

   vrName = Dazzle_name_rsc
   vrIcon = Dazzle_icon_rsc
   vrDesc = Dazzle_desc_rsc

   vrAlreadyEnchanted = Dazzle_already_enchanted
   vrEnchantment_On = Dazzle_on
   vrEnchantment_Off = Dazzle_off
   vrSuccess = Dazzle_caster

   viSpell_num = SID_DAZZLE
   viSchool = SS_SHALILLE
   viSpell_level = 4
   viMana = 12
   viChance_To_Increase = 15
   viMeditate_ratio = 40

   viSpellExertion = 3

   vrSucceed_wav = Dazzle_sound

properties:

   piSpellPowerMultiplier = 40 //103 value 70
   piKarmaPowerMultiplier = 10 // 103 value 50
   piBaseDuration = 3000 // 103 value 3000
   piDeviation = 10 // 103 value 0

   // Min and max values for Dazzle duration.
   piMinDazzle = 3000
   piMaxDazzle = 10000

   // Duration of the red pain effect after spell wears off.
   piPainDuration = 1000 // 103 value 8000

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Emerald,1],plReagents);
      plReagents = Cons([&PurpleMushroom,1],plReagents);

      return;
   }

   DoSpellEffects(who=$,lTargets=$,iDuration=0)
   {
      local oSpell, oTarget;

      oTarget = First(lTargets);

      // Check if AMA blocks dazzle completely.
      if (Send(oTarget,@CheckBattlerFlag,#flag=BFLAG_SPELLPOWER_MOD))
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_ANTIMAGIC_AURA);
         if (Send(oSpell,@TryResistBlind,#who=who,#oTarget=oTarget))
         {
            return TRUE;
         }
      }

      oSpell = Send(SYS,@FindSpellByNum,#NUM=SID_EAGLE_EYES);
      if Send(oTarget,@IsEnchanted,#what=oSpell)
      {
         iDuration = Send(oSpell,@DoEagleEyes,#oCaster=who,#oTarget=oTarget,
                           #iDuration=iDuration,#iFactor=2);
         if iDuration = $
         {
            // Resisted completely.
            return TRUE;
         }
      }

      // Re-check the bound on duration.
      iDuration = Bound(iDuration,piMinDazzle,piMaxDazzle);

      if IsClass(oTarget,&User)
      {
         Send(oTarget,@MsgSendUser,#message_rsc=vrEnchantment_On);
         Send(oTarget,@EffectSendUserDuration,#effect=EFFECT_WHITEOUT,
               #duration=iDuration);
      }
      else
      {
         // Class is Monster
         Post(oTarget,@ResetBehaviorFlags);
      }

      Send(who,@MsgSendUser,#message_rsc=vrSuccess,
            #parm1=Send(oTarget,@GetCapDef),#parm2=Send(oTarget,@GetName));
      Send(oTarget,@StartEnchantment,#what=self,#time=iDuration);

      propagate;
   }

   GetDuration(iSpellPower=0,caster=$,lTargets=$)
   {
      local iDuration, iKarmaDif, iKarma, iTargetKarma, oTarget;

      oTarget = First(lTargets);

      // 1-8 seconds based on spellpower. Recommended 3-7.
      iDuration = piBaseDuration + iSpellPower * piSpellPowerMultiplier;

      if IsClass(caster,&Battler)
      {
         iKarma = Send(caster,@GetKarma);
      }
      else
      {
         // Non battlers, like the sun
         iKarma = Random(30,90);
      }

      iTargetKarma = Send(oTarget,@GetKarma,#detect=TRUE);

      iKarmaDif = iKarma - iTargetKarma;

      // Add 1-10 seconds based on karma difference.
      iDuration = Bound(iDuration + (iKarmaDif * piKarmaPowerMultiplier),
                        piMinDazzle,piMaxDazzle);

      // Add any random duration.
      iDuration = Random(iDuration * (100 - piDeviation) / 100,iDuration);

      return iDuration;
   }

   EndEnchantment(who=$,report=TRUE)
   {
      if IsClass(who,&User)
      {
         if report
         {
            Send(who,@MsgSendUser,#message_rsc=vrEnchantment_Off);
         }

         Send(who,@EffectSendUserDuration,#effect=EFFECT_PAIN,
               #duration=piPainDuration);
         Send(who,@EffectSendUserXLat,#xlat=0);
      }
      else
      {
         // Class is monster
         // Post this so it's done AFTER the enchantment is gone from the
         // monster's ench list
         Post(who,@ResetBehaviorFlags);
      }

      return;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetPlayerFlag,#flag=PFLAG_NO_FIGHT,#value=TRUE);
      Send(who,@SetPlayerFlag,#flag=PFLAG_NO_MAGIC,#value=TRUE);

      return;
   }

   RestartEnchantmentEffect(who=$,state=$)
   {
      local i, iDuration, oList;

      Send(who,@MsgSendUser,#message_rsc=vrEnchantment_On);

      oList = Send(who,@GetEnchantmentList);

      foreach i in oList
      {
         if Nth(i,2) = self
         {
            iDuration = GetTimeRemaining(First(i));
         }
      }

      Send(who,@EffectSendUserDuration,#effect=EFFECT_WHITEOUT,
            #duration=iDuration);

      return;
   }

   ModifyMonsterBehavior(mob=$)
   {
      Send(mob,@SetBehaviorFlag,#flag=AI_MOVE_RANDOM,#value=TRUE);
      Send(mob,@SetBehaviorFlag,#flag=AI_NOFIGHT,#value=TRUE);

      return;
   }

   CausesAggression(iSpellpower=0)
   {
      return iSpellpower / 3;
   }

   CanPayMonsterCosts(lTargets=$)
   {
      local oTarget;

      oTarget = First(lTargets);

      if (IsClass(oTarget,&Player)
            AND Send(oTarget,@CheckPlayerFlag,#flag=PFLAG_NO_FIGHT))
         OR (IsClass(oTarget,&Monster)
            AND Send(oTarget,@CheckMonsterBehavior,#flag=AI_NOFIGHT))
      {
         return FALSE;
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
