// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
SpiritualHammer is Weapon

constants:

   include blakston.khd

resources:

   include spirhamm.lkod

   spirithammer_name_rsc = "spiritual hammer"
   spirithammer_icon_rsc = hammer.bgf
   spirithammer_desc_rsc = \
      "Inherently having holy blessings, this hammer is even more powerful "
      "than the standard variation."
   spirithammer_expires = \
      "Your spiritual hammer disappears in a puff of smoke!"
   spirithammer_window_overlay_rsc = povhamr.bgf
   spirithammer_player_overlay = hamrov.bgf
   spirithammer_time_header_rsc = \
      "\n\nYour spiritual hammer expires in %r%r%r%r%r%r%r."

classvars:
   vrName = spirithammer_name_rsc
   vrIcon = spirithammer_icon_rsc
   vrDesc = spirithammer_desc_rsc

   // Spiritual Hammers are high quality bludgeoning weapons
   viWeaponType = WEAPON_TYPE_BLUDGEON
   viWeaponQuality = WEAPON_QUALITY_HIGH

   viProficiency_Needed = SKID_PROFICIENCY_HAMMER

   viValue_average = 0
   // Light because it's magical.
   viWeight = 15
   viBulk = 60

   vrWeapon_window_overlay = spirithammer_window_overlay_rsc
   vrWeapon_overlay = spirithammer_player_overlay

   viGround_group = 1
   viInventory_group = 3
   viBroken_group = 2

properties:

   piAttack_type = ATK_TYPE_MAGIC+ATK_TYPE_BLUDGEON
   ptLongevity
   piItem_flags = 10
   piHitBonus = 0

messages:

   Constructor(iLongevity=$, iToHitBonus=$)
   {
      local oWeapAtt;

      if iLongevity = $
      {
         // Default is 1 hour.
         iLongevity = 3600000;
      }

      if iToHitBonus <> $
      {
         piHitBonus = iToHitBonus;
      }

      ptLongevity = CreateTimer(self,@WeaponExpires,iLongevity);

      Send(self,@SetSpellFlag,#flag=SPL_TYPE_HOLY,#value=TRUE);

      propagate;
   }

   AppendDesc()
   {
      if ptLongevity <> $
      {
         Send(self,@SendTimeRemaining,
               #iTime=GetTimeRemaining(ptLongevity) / 1000);
      }
      else
      {
         AddPacket(4,system_blank_resource);
      }

      return;
   }

   SendTimeRemainingHeader(parm1=$)
   {
      AddPacket(4,spirithammer_time_header_rsc);

      return;
   }

   WeaponExpires()
   {
      ptLongevity = $;

      if IsClass(poOwner,&User)
      {
         Send(poOwner,@MsgSendUser,#message_rsc=spirithammer_expires);
      }

      Send(self,@Delete);

      return;
   }

   Delete()
   {
      if ptLongevity <> $
      {
         DeleteTimer(ptLongevity);
         ptLongevity = $;
      }

      propagate;
   }

   WeaponHitTarget()
   {
      // Return here so that spiritual hammers don't get damaged.
      return;
   }

   CanEnchant()
   {
      return FALSE;
   }

   CanBeGivenToNPC()
   {
      return FALSE;
   }

   CanShatter()
   {
      return FALSE;
   }

   CanSwap()
   {
      return FALSE;
   }

   CanWeaken()
   {
      return FALSE;
   }

   GetBaseSpellModifier()
   {
      // It's mystically created and temporary.  No penalties.
      return 0;
   }

end
////////////////////////////////////////////////////////////////////////////////
