// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
CorpseNode is Portal

constants:

   include blakston.khd

   CORPSENODE_RANGE = 3

resources:

   include corpnode.lkod

   corpsenode_name_rsc = "rip in space"
   corpsenode_icon_rsc = teleport.bgf
   corpsenode_desc_rsc = \
      "You know not where this leads, for you see naught but insanity through "
      "the gaping rip in reality."
   corpsenode_already_melded = "You have already bonded with the corpsenode."
   corpsenode_nothing = "Nothing seems to happen."
   corpsenode_meld = \
      "You reach out with your mind to touch the rip, and suddenly the universe "
      "seems to collapse around you!  Vertigo strikes, and you fall to your knees.  "
      "Yet, when you regain your equilibrium, you feel the course of magic flow "
      "through your veins."

   uworld_meld_sound = nodeup.ogg

classvars:

   vrName = corpsenode_name_rsc

   viObject_flags = OF_ACTIVATABLE
   viMoveOn_type = MOVEON_TELEPORTER

   viAnimationSpeed = 400

properties:

   vrIcon = corpsenode_icon_rsc
   vrDesc = corpsenode_desc_rsc

   piNode_num = NODE_CORPSENODE
   piDrawFx = DRAWFX_TRANSLUCENT_75

messages:

   Constructor(node_num=0)
   {
      Send(SYS,@AddNode,#node=self,#num=node_num);

      propagate;
   }

   TeleportSomething(what = $,new_row = $,new_col = $)
   {
      if Send(what,@GetNodeList) & piNode_num
      {
         Send(poOwner,@ZapToCorpse,#who=what);
      }
      else
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
               #where=Send(SYS,@FindRoomByNum,#num=RID_UNDERWORLD),
               #new_row=17,#new_col=16,#fine_row=32,#fine_col=32);
      }

      return;
   }

   //// Node stuff

   GetLocationName()
   {
      // We appear in the underworld, but we're not always stored there
      return Send(Send(SYS, @FindRoomByNum, #num = RID_UNDERWORLD), @GetName);
   }

   NodeAppear(where=$,row=0,col=0,fine_row=0,fine_col=0)
   {
      if NOT IsClass(where,&Room)
      {
         Debug("Not a room!");

         return;
      }

      if poOwner <> $
      {
         Debug("Already has an owner!");

         return;
      }

      if row = 0 OR col = 0
      {
         Debug("Bad coordinates!");

         return;
      }

      Send(where,@NewHold,#what=self,#new_row=row,#new_col=col);

      return;
   }

   NodeDisappear()
   "This is used by the three special nodes (Zjiria's Heart, Underworld, "
   "and FeyNode) so that they don't actually leave the system list when "
   "they disappear.  This way, a player's max mana can always be figured "
   "on the fly."
   {
      if poOwner = $
      {
         Debug("NodeDisappear called with null owner!");

         return;
      }

      Send(poOwner,@LeaveHold,#what=self);
      poOwner = $;

      return;
   }

   TryActivate(who=$)
   "Return False only if you want user.kod to send its own error message to user."
   {
      if poOwner = $
      {
         Debug("TryActivate called with nil owner!");

         return TRUE;
      }

      if IsClass(who,&User) AND poOwner = Send(who,@GetOwner)
      {
         if Abs(Send(who,@GetRow) - Send(self,@GetRow)) < CORPSENODE_RANGE
            AND Abs(Send(who,@GetCol) - Send(self,@GetCol)) < CORPSENODE_RANGE
         {
            if piNode_num = 0
            {
               Debug("this mana node was not assigned an ID!  Invalid!");

               return FALSE;
            }

            if Send(who,@GetNodeList) & piNode_num
            {
               Send(who,@MsgSendUser,#message_rsc=corpsenode_already_melded);

               return TRUE;
            }

            Send(self,@Meld,#who=who);
         }
         else
         {
            Send(who,@MsgSendUser,#message_rsc=corpsenode_nothing);
         }
      }

      return TRUE;
   }

   Meld(who=$)
   {
      Send(who,@MsgSendUser,#message_rsc=corpsenode_meld);
      Send(who,@WaveSendUser,#wave_rsc=uworld_meld_sound);
      Send(who,@ConnectManaNode,#node_num=piNode_num);

      return;
   }

   UnMeld(who=$)
   {
      Send(who,@SeverManaNode,#node_num=piNode_num);

      return;
   }

   GetNodeNum()
   {
      return piNode_num;
   }

   GetManaAdjust(who = $, myst = 25)
   "Returns x10 value, caller must scale down to use it."
   {
      return (35 + myst);
   }

   ReqNewOwner(what = $)
   {
      return FALSE;
   }

end
////////////////////////////////////////////////////////////////////////////////
