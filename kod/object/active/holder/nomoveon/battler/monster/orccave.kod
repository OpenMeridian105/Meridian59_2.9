// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
CaveOrc is Monster

constants:

   include blakston.khd

resources:

   include orccave.lkod

   CaveOrc_Koc_name_rsc = "utom"
   CaveOrc_name_rsc = "cave orc"
   CaveOrc_icon_rsc = OrcCave.bgf
   CaveOrc_desc_rsc = \
      "This dirty orc grunt wanders through the caves hungrily in search "
      "of gems and food."

   CaveOrc_dead_icon_rsc = OrcCaveX.bgf
   CaveOrc_dead_name_rsc = "dead cave orc"

   CaveOrc_sound_hit = orc_atkh.ogg
   CaveOrc_sound_miss = cor_atkm.ogg
   CaveOrc_sound_death = cor_dth.ogg
   CaveOrc_sound_aware = cor_awr.ogg

classvars:

   vrKocName = CaveOrc_koc_name_rsc
   vrName = CaveOrc_name_rsc
   vrIcon = CaveOrc_icon_rsc
   vrDesc = CaveOrc_desc_rsc
   vrDead_icon = CaveOrc_dead_icon_rsc
   vrDead_name = CaveOrc_dead_name_rsc

   viTreasure_type = TID_CAVE_ORC

   viSpeed = SPEED_FAST
   viAttack_type = ATK_TYPE_SLASH
   viAttributes = 0
   viLevel = 80
   viDifficulty = 7
   viVisionDistance = 16
   // Attack range of 192, or 3 row/col.
   viMeleeRange = 192
   viKarma = -55
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_MOVE_FLEE_FRIGHTENERS | AI_FIGHT_SWITCHALOT
   viCashmin = 250
   viCashmax = 650
   viWimpy = 4

   vrSound_hit = CaveOrc_sound_hit
   vrSound_miss = CaveOrc_sound_miss
   vrSound_aware = CaveOrc_sound_aware
   vrSound_death = CaveOrc_sound_death

   vbCanSiphon = FALSE

   viBleedChance = 15
   viBleedStrength = 1500
   viBleedDuration = 5000

   // Very used to dark places.
   viNightVision = 150

properties:

   piAnimation = ANIM_NONE

   piReputationGroup = REP_ORC
   
messages:

   Constructed()
   {
      plResistances = [ [-SPL_TYPE_UNHOLY, 40 ],
                        [ATK_TYPE_PIERCE, 20 ],
                        [ATK_TYPE_SLASH, 25 ],
                        [-SPL_TYPE_HOLY, -20 ] ];

      plReputationEnemies = [REP_PSAVAR, REP_KKAVAR, REP_CTAVAR];

      propagate;
   }

   // Used for defense messages
   GetParryAbility(stroke_obj=$)
   {
      if (stroke_obj <> $) and not Send(stroke_obj,@CanParry)
      {
         return 0;
      }

      return (viDifficulty*10)/4;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,100,2,2,2,6);

      return;
   }

   SendMoveAnimation()
   {
      // Make it look like CaveOrc is walking
      // Cycle through "walking" bitmaps (groups 2-6) at 50 ms per frame
      AddPacket(1,ANIMATE_CYCLE,4,50,2,2,2,6);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,75,2,7,2,11,2,1);

         return;
      }

      // if no body animation.
      propagate;
   }

   IsFrightenedOf(target=$)
   {
      return IsClass(target,&GiantRat);
   }

   IsAlliedClass(what=$)
   {
      if IsClass(what,&Orc)
         OR IsClass(what,&OldOrc)
         OR IsClass(what,&OrcWizard)
         OR IsClass(what,&OrcPitBoss)
      {
         return TRUE;
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
