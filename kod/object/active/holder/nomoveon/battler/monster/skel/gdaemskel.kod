// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
GiantDaemonSkeleton is Skeleton

constants:

   include blakston.khd

   DEMENTIA_CHANCE = 20

   // Base percent chance to resist spells.
   MAGIC_RESIST = 50

resources:

   include gdaemskel.lkod

   GiantDaemonSkeleton_koc_name_rsc = "gikochaleoc"
   GiantDaemonSkeleton_name_rsc = "giant daemon skeleton"
   GiantDaemonSkeleton_icon_rsc = gskelbody.bgf
   GiantDaemonSkeleton_desc_rsc = \
      "The sight alone of this abomination of nature is often enough to "
      "paralyze a human.  You shudder to think what dark ritual brought "
      "this creature into being."

   GiantDaemonSkeleton_head = gskelhed4.bgf
   GiantDaemonSkeleton_dead_icon_rsc = gskelX.bgf
   GiantDaemonSkeleton_dead_name_rsc = "pile of bones"

   GiantDaemonSkeleton_sound_miss = skl4_atk.ogg
   GiantDaemonSkeleton_sound_death = skl4_die.ogg
   GiantDaemonSkeleton_sound_aware = skl4_awr.ogg

classvars:

   vrKocName = GiantDaemonSkeleton_koc_name_rsc
   vrName = GiantDaemonSkeleton_name_rsc
   vrIcon = GiantDaemonSkeleton_icon_rsc
   vrDesc = GiantDaemonSkeleton_desc_rsc
   vrDead_icon = GiantDaemonSkeleton_dead_icon_rsc
   vrDead_name = GiantDaemonSkeleton_dead_name_rsc

   viTreasure_type = TID_SKELETON5
   viSpeed = SPEED_FASTER
   viAttack_type = ATK_TYPE_THRUST
   viAttributes = 0
   viLevel = 200
   viDifficulty = 9
   viVisionDistance = 20
   // Attack range of 192, or 3 row/col.
   viMeleeRange = 192
   viKarma = -100
   viDefault_behavior = \
      AI_FIGHT_AGGRESSIVE | AI_FIGHT_HYPERAGGRESSIVE \
      | AI_FIGHT_SWITCHALOT | AI_MOVE_FLEE_FRIGHTENERS
   viCashmin = 5000
   viCashmax = 10000
   vrSound_miss = GiantDaemonSkeleton_sound_miss
   vrSound_aware = GiantDaemonSkeleton_sound_death
   vrSound_death = GiantDaemonSkeleton_sound_aware

   vrHeadIcon = GiantDaemonSkeleton_head

   viBleedChance = 25
   viBleedStrength = 2500
   viBleedDuration = 12000

   viFatigueChance = 15
   viFatigueStrength = 3000
   viFatigueDuration = 8000

   viBonusXP = 100
   viBonusTP = 10

properties:

   piAnimation = ANIM_NONE

messages:

   SetResistances()
   {
      plResistances = [ [ATK_TYPE_PIERCE, 70 ],
                        [ATK_TYPE_THRUST, 70 ],
                        [-SPL_TYPE_UNHOLY, 70 ],
                        [-SPL_TYPE_SHOCK, 70 ],
                        [-SPL_TYPE_FIRE, 70 ],
                        [-SPL_TYPE_COLD, -10 ],
                        [-SPL_TYPE_HOLY, -20 ],
                        [ATK_TYPE_BLUDGEON, -20 ]
                      ];

      return;
   }

   HitSideEffect(what = $, who = $)
   {
      local oSpell;

      oSpell = Send(SYS,@FindSpellByNum,#num=SID_DEMENT);
      if NOT Send(what,@IsEnchanted,#what=oSpell)
         AND Random(1,DEMENTIA_CHANCE) = 1
      {
         if who <> $
         {
            Send(oSpell,@CastSpell,#who=who,#lTargets=[what],#iSpellPower=75,
                  #bItemCast=TRUE);
         }
         else
         {
            Send(oSpell,@DoSpellEffects,#who=self,#lTargets=[what],
                  #iSpellPower=75);
         }
      }

      return;
   }

   SpellResist(oSpell=$,who=$,iSpellpower=$)
   {
      if IsClass(who,&DM)
      {
         return FALSE;
      }

      if Send(oSpell,@IsHarmful)
      {
         if (IsClass(oSpell,&Debuff)
            AND (iSpellPower < 40
               OR Random(0,100) < MAGIC_RESIST))
         {
            return TRUE;
         }
      }

      return FALSE;
   }

   CanMorphTo()
   {
      // Too big/powerful.
      return FALSE;
   }

end
////////////////////////////////////////////////////////////////////////////////
