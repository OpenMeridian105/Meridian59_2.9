// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
KocatanTailorShop is KocatanRoom

constants:

   include blakston.khd

resources:

   include koctail.lkod
  
   room_koctail = koctail.roo
   room_name_koctail = "The Crooked Hem Garment Shop"

   koctail_music = song12.ogg

   koctail_locked_1 = "Tailor Tepal ix'Zonak's living quarters are locked."
   koctail_locked_2 = "This door is locked fast."
   koctail_locked_3 = "A dank stench wafts from beneath this locked iron door."

   koctail_key_disappears = "The heavy iron key disappears!"

classvars:

   vrName = room_name_koctail

   viTeleport_row = 17
   viTeleport_col = 5

   viTerrain_Type = TERRAIN_CITY | TERRAIN_SHOP
   viPermanent_flags = ROOM_NO_COMBAT | ROOM_SAFELOGOFF

properties:

   prRoom = room_koctail
   piRoom_num = RID_KOC_TAILOR

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   prMusic = koctail_music

messages:

   CreateStandardExits()
   {
      plExits = $;

      // To town -- teleport in to (18,4).
      plExits = Cons([ 17, 3, RID_KOC_SOUTH, 30, 35, ROTATE_NONE ],plExits);
      plExits = Cons([ 18, 3, RID_KOC_SOUTH, 30, 35, ROTATE_NONE ],plExits);

      // This is the key door - leave this here.  It will give the locked
      // message if they don't have a key.
      plExits = Cons([ 15, 9, ROOM_LOCKED_DOOR, koctail_locked_2],plExits);
      // Don't need key going the other way.
      plExits = Cons([ 14, 9, RID_KOC_TAILOR, 16, 9, ROTATE_NONE ],plExits);

      plExits = Cons([ 18, 10, ROOM_LOCKED_DOOR, koctail_locked_1],plExits);
      plExits = Cons([ 9, 1, ROOM_LOCKED_DOOR, koctail_locked_2],plExits);
      plExits = Cons([ 9, 2, ROOM_LOCKED_DOOR, koctail_locked_2],plExits);
      plExits = Cons([ 2, 2, ROOM_LOCKED_DOOR, koctail_locked_3],plExits);
      plExits = Cons([ 1, 2, ROOM_LOCKED_DOOR, koctail_locked_3],plExits);

      plExits = Cons([ 3, 1, RID_KOC_SEWER2, 43, 47, ROTATE_NONE ],plExits);
      plExits = Cons([ 3, 2, RID_KOC_SEWER2, 43, 47, ROTATE_NONE ],plExits);

      propagate;
   }

   CreateStandardObjects()
   {
      local lObjCoords1, lObjCoords2;
      lObjCoords1 = [
               [16,03,24,24,OO_DRESS1],[17,03,08,24,OO_DRESS3],
               [16,07,16,32,OO_DRESS3],[16,05,08,48,OO_JUG],
               [19,04,08,48,OO_CLOTH_BOLT1],[19,03,48,08,OO_CLOTH_BOLT2],
               [19,07,48,48,OO_CLOTH_BOLT3],[19,10,32,32,OO_CLOTH_BOLT4],
               [18,08,40,40,OO_CLOTH_BOLT5],
               [18,10,56,32,OO_CLOTH_BOLT6],[11,10,48,08,OO_GREY_JUG],
               [10,10,56,08,OO_POT],[10,10,32,08,OO_GOLD_BOTTLE]
               ];
      lObjCoords2 = [
               [16,3,48,56,OO_STOOL_KOCATAN],[11,10,48,08,OO_GREY_JUG],
               [10,10,56,08,OO_POT],[10,10,32,08,OO_GOLD_BOTTLE]
               ];
      Send(self,@CreateOrnObjFromList,#lObjlist=lObjCoords1);
      Send(self,@CreateOrnObjFromList,#lObjlist=lObjCoords2);

      Send(self,@NewHold,#what=Create(&KocatanTailor),
            #new_row=18,#new_col=7,#fine_row=40,#fine_col=0,
            #new_angle=ANGLE_EAST);

      propagate;
   }

   SomethingTryGo(what=$,row=$,col=$)
   {
      local i, each_obj;

      if (IsClass(what,&User)
         AND (row = 15 AND col = 9))
      {
         foreach i in Send(what,@GetHolderPassive)
         {
            each_obj = Send(what,@HolderExtractObject,#data=i);
            if IsClass(each_obj,&SanctuaryKey)
            {
               Send(each_obj,@Delete);
               Send(what,@MsgSendUser,#message_rsc=koctail_key_disappears);
               Send(SYS,@UtilGoToSquare,#what=what,#where=self,
                     #new_row=13,#new_col=9);

               return TRUE;
            }
         }
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
