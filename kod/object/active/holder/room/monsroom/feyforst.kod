// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
FeyForest is MonsterRoom

constants:

   include blakston.khd

   // Base light value for the room.
   BASE_LIGHT = LIGHT_NICE

   // What we adjust the Karma in the room by to get the light modifier.
   LIGHT_KARMA_ADJUST = -70

resources:

   feyforest_music_neutral = neutfor.ogg
   feyforest_music_evil = badfor.ogg
   feyforest_music_good = goodfor.ogg

classvars:

   // This is where this section of the faerie forest will
   // 'aim' for on the karmic scale.
   viEquilibrium = 50

   viTerrain_type = TERRAIN_FOREST | TERRAIN_MYSTICAL

   viWeatherZone = WEATHER_ZONE_VALE

properties:

   piBaseLight = BASE_LIGHT
   piOutside_factor = 8

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   piGen_time = 20000
   piGen_percent = 80

   // Room Karma is the karma of the Faerie Forest
   // and determines the state of the forest.
   piRoom_Karma = 50
   ptFaerie = $

   pbSnowGroundTexture = TRUE

messages:

   Constructor()
   {
      piRoom_Karma = viEquilibrium;
      ptFaerie = CreateTimer(self,@FaerieTimer,piGen_time/2);

      propagate;
   }

   FaerieTimer()
   {
      if NOT pbUser_in_room
      {
         if piRoom_Karma < viEquilibrium
         {
            piRoom_Karma = piRoom_Karma + Random(1,5);
         }
         else
         {
            piRoom_Karma = piRoom_Karma - Random(1,5);
         }

         if piInit_count_max < Length(plGenerators)
         {
            ++piInit_count_min;
            ++piInit_count_max;
         }
      }

      ptFaerie = CreateTimer(self,@FaerieTimer,piGen_time/2);

      return;
   }

   Delete()
   {
      if ptFaerie <> $
      {
         DeleteTimer(ptFaerie);
         ptFaerie = $;
      }

      propagate;
   }

   SomethingKilled(victim = $)
   {
      if victim = $
      {
         Debug("SomethingKilled() reached with no victim!");
      }

      if IsClass(victim,&Fairy)
         OR IsClass(victim,&EvilFairy)
      {
         Post(self,@FigureRoomKarma);
      }

      propagate;
   }

   LastUserLeft()
   {
      piInit_count_min = piMonster_count - 2;
      piInit_count_max = piMonster_count + 2;

      propagate;
   }

   TryCreateMonster(initroom = FALSE,goodguy=FALSE,darkguy=FALSE,nofigure=FALSE)
   {
      local oMonster, iRoll, iTotal, lMonster_info, iRow, iCol, lPos;

      if piMonster_count >= piMonster_count_max
      {
         return FALSE;
      }

      iRoll = Random(1,100);
      if (iRoll > piGen_percent) AND (initroom = FALSE)
      {
         return FALSE;
      }

      iRoll = Random(1,100);
      iRoll += (Bound((piRoom_Karma - viEquilibrium)/5,-10,10));

      if (iRoll >= piRoom_Karma AND NOT darkguy) OR goodguy
      {
         // Forest is evil.  Make it good.
         oMonster = Create(&Fairy);
      }
      else
      {
         // Forest is good.  Make it evil.
         oMonster = Create(&EvilFairy);
      }

      if plGenerators = $
      {
         iRow = Random(1,piRows);
         iCol = Random(1,piCols);
      }
      else
      {
         iRoll = Random(1,Length(plGenerators));
         lPos = Nth(plGenerators,iRoll);
         iRow = First(lPos);
         iCol = Nth(lPos,2);
      }

      if Send(self,@ReqNewHold,#what=oMonster,#new_row=iRow,#new_col=iCol)
         AND Send(self,@ReqSomethingMoved,#what=oMonster,#new_row=iRow,#new_col=iCol)
      {
         Send(self,@NewHold,#what=oMonster,#new_row=iRow,#new_col=iCol,
              #fine_row=FINENESS/2,#fine_col=FINENESS/2);
      }
      else
      {
         Send(oMonster,@Delete);

         return FALSE;
      }

      if NOT noFigure
      {
         Send(self,@FigureRoomKarma);
      }

      return TRUE;
   }

   FigureRoomKarma(bForceChange=FALSE)
   {
      local i, gCount, eCount, oldKarma, iIndex, cFairy;

      gCount = 0;
      eCount = 0;
      oldKarma = Send(self,@GetKarma);

      foreach i in plActive
      {
         cFairy = GetClass(First(i));
         if (cFairy = &Fairy)
         {
            ++gCount;
         }
         else if (cFairy = &EvilFairy)
         {
            ++eCount;
         }
      }

      if gCount = 0 AND eCount = 0
      {
         gCount = 1;
         eCount = 1;
      }

      piRoom_Karma = (100 * gCount) / (gCount+eCount);

      // enough have died for a change in the forest.
      if Send(self,@GetKarma) <> oldKarma
         OR bForceChange
      {
         Send(self,@SetRoomKarma,#karma=Send(self,@GetKarma));
      }

      return;
   }

   GetKarma()
   {
      if piRoom_Karma < 20 { return KVERY_EVIL; }
      if piRoom_Karma < 40 { return KEVIL; }
      if piRoom_Karma > 80 { return KVERY_GOOD; }
      if piRoom_Karma > 60 { return KGOOD; }

      return KNEUTRAL;
   }

   SetRoomKarma(karma = $)
   {
      local karma1, karma2, karma3, oCenter,rMusic,i,each_obj;

      rMusic = feyforest_music_neutral;

      if karma = KVERY_EVIL
      {
         rMusic = feyforest_music_evil;
      }
      else if karma = KVERY_GOOD
      {
         rMusic = feyforest_music_good;
      }

      prMusic = rMusic;

      foreach i in plActive
      {
         each_obj = First(i);
         if IsClass(each_obj,&User)
         {
            Send(each_obj,@SendRoomMusic,#music_rsc=rMusic);
         }
      }

      foreach i in plPassive
      {
         each_obj = First(i);
         if IsClass(each_obj,&FeyTree)
         {
            Send(each_obj,@SetKarma,#karma=karma);
         }
      }

      karma1 = 8871;
      karma2 = 8860 + ((karma - 1) * 2) + 1;
      karma3 = 8860 + (karma * 2);

      Send(self,@ChangeTexture,#id=2,#new_texture=karma2,#flags=CTF_NORMALWALL);
      Send(self,@ChangeTexture,#id=3,#new_texture=karma3,#flags=CTF_NORMALWALL);

      if (karma = KVERY_EVIL)
      {
         Send(self,@ChangeTexture,#id=1,#new_texture=karma1,#flags=CTF_NORMALWALL);
      }
      else
      {
         Send(self,@ChangeTexture,#id=1,#new_texture=karma2,#flags=CTF_NORMALWALL);
      }

      // See if we need to load the node.
      oCenter = Send(SYS,@FindRoomByNum,#num=RID_C2);
      Send(oCenter,@KarmaChanged,#karma=karma);

      // Lighten/darken the room as appropriate.
      Send(self,@SetBaseLight,#amount=BASE_LIGHT+piRoom_Karma+LIGHT_KARMA_ADJUST);

      return;
   }

   SomeoneSaid(what=$,string=$,type=$)
   {
      if type <> SAY_DM
         OR NOT IsClass(what,&DM)
      {
         propagate;
      }

      // Codes to spawn mobs.
      if StringEqual(string,"good fey")
      {
         Send(self,@TryCreateMonster,#goodguy=TRUE);
      }
      else if StringEqual(string,"evil fey")
      {
         Send(self,@TryCreateMonster,#darkguy=TRUE);
      }

      propagate;
   }

   CreateYellZoneList()
   {
      plYell_zone = [RID_A1, RID_B1, RID_B2, RID_C1,
                     RID_C2, RID_C3, RID_D2, RID_D1];

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
