// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


///////////////////////////////////////////////////////////////////////////
RealTime is UtilityFunctions

// The RealTime object provides functions for returning the real time in
// seconds, minutes, hours, days, month and year. It also keeps a list of
// messages to call back at a given time, in plCallBacks. Messages can be
// registered in this list using RegisterCallBack (or variants of this
// message) and passing in the time, object, message name to call and
// up to 15 parameters in the form parm1-parm15.
// A timer goes off every minute and if any callbacks are due to be called,
// the stored message is sent to the stored object.

constants:

   include blakston.khd

resources:

classvars:

properties:

   ptTick = $
   plCallbacks = $

messages:

   Constructor()
   {
      ptTick = CreateTimer(self,@TimeTick,60000);

      return;
   }

   Recreate()
   {
      return;
   }

   TimeTick()
   {
      ptTick = CreateTimer(self,@TimeTick,60000);
      Send(self,@NewMinute);

      return;
   }

   Delete()
   {
      DeleteTimer(ptTick);

      plCallBacks = $;

      return;
   }

   RegisterCallback(iHour=0,iMinute=0,iSecond=0,iMonth=0,iDay=0,iYear=0,
                    oObject=$,message=$,parm1=$,parm2=$,parm3=$,parm4=$,
                    parm5=$,parm6=$,parm7=$,parm8=$,parm9=$,parm10=$,parm11=$,
                    parm12=$,parm13=$,parm14=$,parm15=$)
   {
      // First check if we have a valid message to call later on.
      if message = $
      {
         return;
      }

      if NOT IsString(message)
      {
         message = SetString($,message);
      }

      plCallbacks = Cons([[iMonth,iDay,iYear,iHour,iMinute,iSecond],
                        oObject,message,parm1,parm2,parm3,parm4,parm5,
                        parm6,parm7,parm8,parm9,parm10,parm11,parm12,
                        parm13,parm14,parm15],plCallbacks);

      return;
   }

   RegisterCallbackInMinutes(minutes=$,oObject=$,message=$,parm1=$,parm2=$,
                              parm3=$,parm4=$,parm5=$,parm6=$,parm7=$,parm8=$,
                              parm9=$,parm10=$,parm11=$,parm12=$,parm13=$,
                              parm14=$,parm15=$)
   {
      local iYear, iMonth, iDay, iHour, iMinute, iMonthDays;

      if oObject = $
         OR minutes = $
         OR message = $
      {
         return;
      }

      // Check if the messages is in string format, if not, make it.
      if NOT IsString(message)
      {
         message = SetString($,message);
      }

      GetDateAndTime(BTIME_LOCAL, *iYear, *iMonth, *iDay, *iHour, *iMinute, $);

      if Send(self,@IsLeapYear,#iYear=iYear)
      {
         iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
      }
      else
      {
         iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
      }

      iMinute += minutes;

      // Fix any rollovers.
      while iMinute >= 60
      {
         iMinute = iMinute - 60;
         iHour = iHour + 1;

         while iHour >= 24
         {
            iHour = iHour - 24;
            iDay = iDay + 1;
      
            while iDay > Nth(iMonthDays,iMonth)
            {
               iDay = iDay - Nth(iMonthDays,iMonth);
               iMonth = iMonth + 1;

               while iMonth > 12
               {
                  iMonth = iMonth - 12;
                  iYear = iYear + 1;
                  if Send(self,@IsLeapYear,#iYear=iYear)
                  {
                     iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
                  }
                  else
                  {
                     iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
                  }
               }
            }
         }
      }

      plCallbacks = Cons([[iMonth,iDay,iYear,iHour,iMinute,0],
                        oObject,message,parm1,parm2,parm3,parm4,parm5,
                        parm6,parm7,parm8,parm9,parm10,parm11,parm12,
                        parm13,parm14,parm15],plCallbacks);

      return;
   }

   RegisterCallbackInHours(hours=$,oObject=$,message=$,parm1=$,parm2=$,
                           parm3=$,parm4=$,parm5=$,parm6=$,parm7=$,parm8=$,
                           parm9=$,parm10=$,parm11=$,parm12=$,parm13=$,
                           parm14=$,parm15=$)
   {
      local iYear, iMonth, iDay, iHour, iMinute, iMonthDays;

      if oObject = $
         OR hours = $
         OR message = $
      {
         return;
      }

      // Check if the messages is in string format, if not, make it.
      if NOT IsString(message)
      {
         message = SetString($,message);
      }

      GetDateAndTime(BTIME_LOCAL, *iYear, *iMonth, *iDay, *iHour, *iMinute, $);

      if Send(self,@IsLeapYear,#iYear=iYear)
      {
         iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
      }
      else
      {
         iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
      }

      iHour += hours;

      // Fix any rollovers.
      while iHour >= 24
      {
         iHour = iHour - 24;
         iDay = iDay + 1;

         while iDay > Nth(iMonthDays,iMonth)
         {
            iDay = iDay - Nth(iMonthDays,iMonth);
            iMonth = iMonth + 1;

            while iMonth > 12
            {
               iMonth = iMonth - 12;
               iYear = iYear + 1;
               if Send(self,@IsLeapYear,#iYear=iYear)
               {
                  iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
               }
               else
               {
                  iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
               }
            }
         }
      }

      plCallbacks = Cons([[iMonth,iDay,iYear,iHour,iMinute,0],
                        oObject,message,parm1,parm2,parm3,parm4,parm5,
                        parm6,parm7,parm8,parm9,parm10,parm11,parm12,
                        parm13,parm14,parm15],plCallbacks);

      return;
   }

   RegisterCallbackInDays(days=$,oObject=$,message=$,parm1=$,parm2=$,
                        parm3=$,parm4=$,parm5=$,parm6=$,parm7=$,parm8=$,
                        parm9=$,parm10=$,parm11=$,parm12=$,parm13=$,
                        parm14=$,parm15=$)
   {
      local iYear,iMonth,iDay,iHour,iMinute,iMonthDays;

      if oObject = $
         OR days = $
         OR message = $
      {
         return;
      }

      // Check if the messages is in string format, if not, make it.
      if NOT IsString(message)
      {
         message = SetString($,message);
      }

      GetDateAndTime(BTIME_LOCAL, *iYear, *iMonth, *iDay, *iHour, *iMinute, $);

      if Send(self,@IsLeapYear,#iYear=iYear)
      {
         iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
      }
      else
      {
         iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
      }

      iDay += days;

      // Fix any rollovers.
      while iDay > Nth(iMonthDays,iMonth)
      {
         iDay = iDay - Nth(iMonthDays,iMonth);
         iMonth = iMonth + 1;

         while iMonth > 12
         {
            iMonth = iMonth - 12;
            iYear = iYear + 1;
            if Send(self,@IsLeapYear,#iYear=iYear)
            {
               iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
            }
            else
            {
               iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
            }
         }
      }

      plCallbacks = Cons([[iMonth,iDay,iYear,iHour,iMinute,0],
                        oObject,message,parm1,parm2,parm3,parm4,parm5,
                        parm6,parm7,parm8,parm9,parm10,parm11,parm12,
                        parm13,parm14,parm15],plCallbacks);

      return;
   }

   RegisterCallbackInMonths(months=$,oObject=$,message=$,parm1=$,parm2=$,
                           parm3=$,parm4=$,parm5=$,parm6=$,parm7=$,parm8=$,
                           parm9=$,parm10=$,parm11=$,parm12=$,parm13=$,
                           parm14=$,parm15=$)
   {
      local iYear, iMonth, iDay, iHour, iMinute, iMonthDays;

      if oObject = $
         OR months = $
         OR message = $
      {
         return;
      }

      // Check if the messages is in string format, if not, make it.
      if NOT IsString(message)
      {
         message = SetString($,message);
      }

      GetDateAndTime(BTIME_LOCAL, *iYear, *iMonth, *iDay, *iHour, *iMinute, $);

      if Send(self,@IsLeapYear,#iYear=iYear)
      {
         iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
      }
      else
      {
         iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
      }

      iMonth += months;

      // Fix any rollovers.
      while iMonth > 12
      {
         iMonth = iMonth - 12;
         iYear = iYear + 1;
         if Send(self,@IsLeapYear,#iYear=iYear)
         {
            iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
         }
         else
         {
            iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
         }
      }

      // Second pass in case changing the month screwed up the day.
      while iDay > Nth(iMonthDays,iMonth)
      {
         iDay = iDay - Nth(iMonthDays,iMonth);
         iMonth = iMonth + 1;

         while iMonth > 12
         {
            iMonth = iMonth - 12;
            iYear = iYear + 1;
            if Send(self,@IsLeapYear,#iYear=iYear)
            {
               iMonthDays = [31,29,31,30,31,30,31,31,30,31,30,31];
            }
            else
            {
               iMonthDays = [31,28,31,30,31,30,31,31,30,31,30,31];
            }
         }
      }

      plCallbacks = Cons([[iMonth,iDay,iYear,iHour,iMinute,0],
                        oObject,message,parm1,parm2,parm3,parm4,parm5,
                        parm6,parm7,parm8,parm9,parm10,parm11,parm12,
                        parm13,parm14,parm15],plCallbacks);

      return;
   }

   RegisterCallbackInYears(years=$,oObject=$,message=$,parm1=$,parm2=$,
                           parm3=$,parm4=$,parm5=$,parm6=$,parm7=$,parm8=$,
                           parm9=$,parm10=$,parm11=$,parm12=$,parm13=$,
                           parm14=$,parm15=$)
   {
      local iYear, iMonth, iDay, iHour, iMinute, iMonthDays;

      if oObject = $
         OR years = $
         OR message = $
      {
         return;
      }

      // Check if the messages is in string format, if not, make it.
      if NOT IsString(message)
      {
         message = SetString($,message);
      }

      GetDateAndTime(BTIME_LOCAL, *iYear, *iMonth, *iDay, *iHour, *iMinute, $);

      iYear += years;

      // Check for a possible issue caused by a leap year.
      if NOT Send(self,@IsLeapYear,#iYear=iYear)
         AND iMonth = 2
         AND iDay = 29
      {
         iDay = 28;
      }

      plCallbacks = Cons([[iMonth,iDay,iYear,iHour,iMinute,0],
                        oObject,message,parm1,parm2,parm3,parm4,parm5,
                        parm6,parm7,parm8,parm9,parm10,parm11,parm12,
                        parm13,parm14,parm15],plCallbacks);

      return;
   }

   ClearCallbacks()
   {
      plCallbacks = $;

      return;
   }

   DeleteCallback(what = $)
   "Deletes all callbacks matching the given object. Returns number deleted."
   {
      local i, iDeleted;

      iDeleted = 0;

      foreach i in plCallbacks
      {
         if (Nth(i,2) = what)
         {
            plCallbacks = DelListElem(plCallbacks,i);
            ++iDeleted;
         }
      }

      return iDeleted;
   }

   NewMinute()
   {
      local lCallbackTime, i, iYear, iMonth, iDay, iHour, iMinute;

      Send(EVENTENGINE_OBJECT,@NewMinute);

      GetDateAndTime(BTIME_LOCAL, *iYear, *iMonth, *iDay, *iHour, *iMinute, $);

      if (iMinute = 0)
      {
         Send(self,@NewHour);
      }

      if plCallbacks <> $
      {
         foreach i in plCallbacks
         {
            lCallbackTime = First(i);

            if Nth(lCallbackTime,3) <= iYear
               AND First(lCallbackTime) <= iMonth
               AND Nth(lCallbackTime,2) <= iDay
               AND Nth(lCallbackTime,4) <= iHour
            {
               if Nth(lCallbackTime,5) = iMinute
               {
                     plCallbacks = DelListElem(plCallbacks,i);
                     Send(Nth(i,2),Nth(i,3),
                        #parm1=Nth(i,4),#parm2=Nth(i,5),#parm3=Nth(i,6),
                        #parm4=Nth(i,7),#parm5=Nth(i,8),#parm6=Nth(i,9),
                        #parm7=Nth(i,10),#parm8=Nth(i,11),#parm9=Nth(i,12),
                        #parm10=Nth(i,13),#parm11=Nth(i,14),#parm12=Nth(i,15),
                        #parm13=Nth(i,16),#parm14=Nth(i,17),#parm15=Nth(i,18));
               }
               else if Nth(lCallbackTime,5) < iMinute
               {
                  Debug("Deleting expired event that would never have started! (event time is less than current time)");
                  plCallbacks = DelListElem(plCallbacks,i);
               }
            }
         }
      }

      return;
   }

   NewHour()
   {
      Send(EVENTENGINE_OBJECT,@NewHour);

      if (Send(self,@GetHour) = 0)
      {
         Send(self,@NewDay);
      }

      return;
   }

   NewDay()
   {
      Send(EVENTENGINE_OBJECT,@NewDay);

      // Temporary stats reset tokens expire after a set number
      // of days. Use real days for ease of calculation/less calls.
      Send(&StatsResetToken,@NewDay);

      if (Send(self,@GetDay) = 1)
      {
         Send(self,@NewMonth);
      }

      return;
   }

   NewMonth()
   {
      Send(EVENTENGINE_OBJECT,@NewMonth);

      if (Send(self,@GetMonth) = 1)
      {
         Send(self,@NewYear);
      }

      return;
   }

   NewYear()
   {
      Send(EVENTENGINE_OBJECT,@NewYear);

      return;
   }

   GetYear()
   {
      local iYear;

      GetDateAndTime(BTIME_LOCAL, *iYear, $, $, $, $, $);

      return iYear;
   }

   GetMonth()
   {
      local iMonth;

      GetDateAndTime(BTIME_LOCAL, $, *iMonth, $, $, $, $);

      return iMonth;
   }

   GetDay()
   {
      local iDay;

      GetDateAndTime(BTIME_LOCAL, $, $, *iDay, $, $, $);

      return iDay;
   }

   GetHour()
   {
      local iHour;

      GetDateAndTime(BTIME_LOCAL, $, $, $, *iHour, $, $);

      return iHour;
   }

   GetMinute()
   {
      local iMinute;

      GetDateAndTime(BTIME_LOCAL, $, $, $, $, *iMinute, $);

      return iMinute;
   }

   GetSecond()
   {
      local iSecond;

      GetDateAndTime(BTIME_LOCAL, $, $, $, $, $, *iSecond);

      return iSecond;
   }

   IsLeapYear(iYear=1901)
   {
      if (iYear % 4 = 0
            AND iYear % 100 <> 0)
         OR iYear % 400 = 0
      {
         return TRUE;
      }

      return FALSE;
   }

end
///////////////////////////////////////////////////////////////////////////
